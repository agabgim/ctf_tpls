# This will configure and build parmetis
# User can configure the source path by speficfying PARMETIS_SRC_DIR,
#    the download path by specifying PARMETIS_URL, or the installed 
#    location by specifying PARMETIS_INSTALL_DIR


# Intialize download/src/install vars
SET( PARMETIS_BUILD_DIR "${CMAKE_BINARY_DIR}/PARMETIS-prefix/src/PARMETIS-build" )
IF ( PARMETIS_URL ) 
    MESSAGE_TPL("   PARMETIS_URL = ${PARMETIS_URL}")
    SET( PARMETIS_CMAKE_URL            "${PARMETIS_URL}"       )
    SET( PARMETIS_CMAKE_DOWNLOAD_DIR   "${PARMETIS_BUILD_DIR}" )
    SET( PARMETIS_CMAKE_SOURCE_DIR     "${PARMETIS_BUILD_DIR}" )
    SET( PARMETIS_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/parmetis" )
    SET( CMAKE_BUILD_PARMETIS TRUE )
ELSEIF ( PARMETIS_SRC_DIR )
    VERIFY_PATH("${PARMETIS_SRC_DIR}")
    MESSAGE_TPL("   PARMETIS_SRC_DIR = ${PARMETIS_SRC_DIR}")
    SET( PARMETIS_CMAKE_URL            "${PARMETIS_SRC_DIR}"   )
    SET( PARMETIS_CMAKE_DOWNLOAD_DIR   "${PARMETIS_BUILD_DIR}" )
    SET( PARMETIS_CMAKE_SOURCE_DIR     "${PARMETIS_BUILD_DIR}" )
    SET( PARMETIS_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/parmetis" )
    SET( CMAKE_BUILD_PARMETIS TRUE )
ELSEIF ( PARMETIS_INSTALL_DIR ) 
    SET( PARMETIS_CMAKE_INSTALL_DIR "${PARMETIS_INSTALL_DIR}" )
    SET( CMAKE_BUILD_PARMETIS FALSE )
ELSE()
    MESSAGE(FATAL_ERROR "Please specify PARMETIS_SRC_DIR, PARMETIS_URL, or PARMETIS_INSTALL_DIR")
ENDIF()
SET( PARMETIS_INSTALL_DIR "${PARMETIS_CMAKE_INSTALL_DIR}" )
MESSAGE_TPL( "   PARMETIS_INSTALL_DIR = ${PARMETIS_INSTALL_DIR}" )
FILE( APPEND "${CMAKE_INSTALL_PREFIX}/TPLs.cmake" "SET(PARMETIS_INSTALL_DIR \"${PARMETIS_INSTALL_DIR}\")\n" )


# Configure parmetis
IF ( CMAKE_BUILD_PARMETIS )
    IF ( ENABLE_SHARED AND ENABLE_STATIC )
        MESSAGE(FATAL_ERROR "Compiling parmetis with both static and shared libraries is not yet supported")
    ELSEIF ( ENABLE_SHARED )
        SET( PARMETIS_CFLAGS "${CMAKE_C_FLAGS} -shared" )
        SET( PARMETIS_CXXFLAGS "${CMAKE_CXX_FLAGS} -shared" )
        SET( PARMETIS_FFLAGS "${CMAKE_Fortran_FLAGS} -shared" )
    ELSEIF ( ENABLE_STATIC )
        SET( PARMETIS_CFLAGS "${CMAKE_C_FLAGS} -static" )
        SET( PARMETIS_CXXFLAGS "${CMAKE_CXX_FLAGS} -static" )
        SET( PARMETIS_FFLAGS "${CMAKE_Fortran_FLAGS} -static" )
    ENDIF()
    SET( PARMETIS_VARS CC=${CMAKE_C_COMPILER} CFLAGS=${PARMETIS_CFLAGS} )
    SET( PARMETIS_VARS ${PARMETIS_VARS} CXX=${CMAKE_CXX_COMPILER} CXXFLAGS=${PARMETIS_CXXFLAGS} )
    SET( PARMETIS_VARS ${PARMETIS_VARS} FC=${CMAKE_Fortran_COMPILER} FCFLAGS=${PARMETIS_FFLAGS} )
    SET( PARMETIS_VARS ${PARMETIS_VARS} LDFLAGS=${LDFLAGS} )
ENDIF()


# Build parmetis
IF ( CMAKE_BUILD_PARMETIS )
    EXTERNALPROJECT_ADD(
        PARMETIS
        URL                 "${PARMETIS_CMAKE_URL}"
        DOWNLOAD_DIR        "${PARMETIS_CMAKE_DOWNLOAD_DIR}"
        SOURCE_DIR          "${PARMETIS_CMAKE_SOURCE_DIR}"
        UPDATE_COMMAND      ""
        CONFIGURE_COMMAND   make config CC=${CMAKE_C_COMPILER} CXX=${CMAKE_CXX_COMPILER} 
                            FC=${CMAKE_Fortran_COMPILER} prefix=${PARMETIS_SRC_DIR}/parmetis VERBOSE=1
        BUILD_COMMAND       make ${PARMETIS_VARS} -i
        BUILD_IN_SOURCE     1
        INSTALL_COMMAND     make install -i
        LOG_DOWNLOAD 1   LOG_UPDATE 1   LOG_CONFIGURE 1   LOG_BUILD 1   LOG_TEST 1   LOG_INSTALL 1
    )
    ADD_TPL_SAVE_LOGS( PARMETIS )
    ADD_TPL_CLEAN( PARMETIS )
ELSE()
    ADD_TPL_EMPTY( PARMETIS )
ENDIF()


